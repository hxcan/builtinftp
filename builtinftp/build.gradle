apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version="2021.9.11"

android {
    compileSdkVersion 28

    resourcePrefix "stupidbeauty__"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 751
        versionName version
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies
        {
            api 'com.google.protobuf:protobuf-java:3.11.4'

//            compile 'com.android.support:appcompat-v7:28.0.0'
            compile 'com.koushikdutta.async:androidasync:2.1.7'

        }


def siteUrl='https://stupidbeauty.com/Blog/'
def hgUrl='https://bitbucket.org/hxcan/nqna/src/default/'
group="com.stupidbeauty"


install {
    repositories.mavenInstaller {
        pom {
            //noinspection GroovyAssignabilityCheck
            project {
                packaging 'aar'
                //noinspection GroovyAssignabilityCheck
                name 'nqna'
                url siteUrl

                licenses {
                    license {
                        //noinspection GroovyAssignabilityCheck
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'hxcan'
                        //noinspection GroovyAssignabilityCheck
                        name 'Hxcan Cai'
                        email 'caihuosheng@gmail.com'
                    }
                }

                scm {
                    connection hgUrl
                    developerConnection hgUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    //noinspection GroovyAssignabilityCheck
    from android.sourceSets.main.java.srcDirs
    classifier='sources'
}

task javadoc(type: Javadoc) {
    source=android.sourceSets.main.java.srcDirs
    //noinspection GroovyAssignabilityCheck
    classpath+=project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier='javadoc'
    //noinspection GroovyAssignabilityCheck
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties=new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user=properties.getProperty("bintray.user")
    key=properties.getProperty("bintray.apikey")
    configurations=['archives']
    pkg {
        repo="maven"
        name="nqna"
        websiteUrl=siteUrl
        vcsUrl=hgUrl
        licenses=["Apache-2.0"]
        publish=true
    }
}
